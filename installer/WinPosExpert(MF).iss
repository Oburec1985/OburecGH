; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
OutputDir=.\Output
OutputBaseFilename=winpos-3.1.22.11-expert-setup
AppName=WinПОС
AppVerName=WinПОС 3.1 Expert
AppPublisher=НПП "Мера"
AppPublisherURL=http://www.nppmera.ru
AppSupportURL=http://www.nppmera.ru/
AppUpdatesURL=http://www.nppmera.ru/
DefaultDirName={pf}\MERA\WinPOS
UsePreviousAppDir=yes
DefaultGroupName=MERA\WinPOS
AllowNoIcons=yes
;AlwaysRestart=yes
LicenseFile=.\License\License.txt
;InfoBeforeFile=.\License\BeforeInstall.txt
;InfoAfterFile=.\License\AfterInstall.txt
InfoBeforeFile=.\License\AfterInstall.txt
WizardImageFile=.\BMP\WPSetup.bmp
WizardSmallImageFile=.\BMP\WPSetupSmall.BMP
ChangesAssociations=yes
SolidCompression=yes
Compression=lzma
PrivilegesRequired=admin
; uncomment the following line if you want your installation to run on NT 3.51 too.
; MinVersion=4,3.51

[Languages]
;Name: en; MessagesFile: "compiler:Default.isl"
Name: ru; MessagesFile: "compiler:Languages\Russian.isl"

[Tasks]
Name: "desktopicon"; Description: "Создавать ярлык на рабочем столе"; GroupDescription: "Дополнительные ярлыки:"; MinVersion: 4,4
Name: "quicklaunchicon"; Description: "Добавлять ярлык в панель быстрого запуска"; GroupDescription: "Дополнительные ярлыки:"; MinVersion: 4,4

[Components]
Name: "program"; Description: "Основные файлы WinПОС"; Types: full compact custom; Flags: fixed; ExtraDiskSpaceRequired: 1048576
Name: "guardant"; Description: "Драйвера Guardant"; Types: full compact custom
Name: "help";    Description: "Файлы справки"; Types: full compact custom
Name: "samples"; Description: "Примеры сценариев"; Types: full compact custom
Name: "manualu"; Description: "Руководство пользователя (pdf)"; Types: full custom
Name: "manualp"; Description: "Руководство программиста (pdf)"; Types: full custom
Name: "info"; Description: "Дополнительная информация об НПП Мера"; Types: full custom
Name: "info\catalog"; Description: "Каталог продукции (pdf)"; Types: full
;Name: "info\present"; Description: "Презентация (Power Point)"; Types: full

[Dirs]
Name: "{sd}\Mera Files"; Permissions: authusers-modify
Name: "{sd}\Mera Files\WinPOS"; Permissions: authusers-modify
;Name: "{commonappdata}\MERA\WinPOS"; Permissions: authusers-modify

[Files]
Source: ".\Protect\Exp\WinPos.exe"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: ".\Protect\Exp\GrdVkc32.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion

Source: "..\Release\wpGraphs.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\wpOperBase.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\wpOperators.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\wpOperProps.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\wpMLOpers.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\wpVibroOpers.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\wpVOperProps.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\meUnits.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion regserver
Source: "..\Release\wpd.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\Scintilla.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\SciLexer.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Debug\WinPos.tlb"; DestDir: "{app}"; Components: program; Flags: ignoreversion regtypelib
;Source: "..\Release\MeraTable.dll"; DestDir: "{app}"; Components: program; Flags: ignoreversion regserver

Source: ".\Script\pdm.dll"; DestDir: "{sys}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall regserver
Source: ".\dll\MSCOMCTL.OCX"; DestDir: "{sys}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall regserver
Source: ".\dll\mfc90.dll"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\msvcr90.dll"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\msvcp90.dll"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\msjava.dll"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\atl90.dll"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\vcomp90.dll"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\Microsoft.VC90.OpenMP.manifest"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\Microsoft.VC90.ATL.manifest"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\Microsoft.VC90.CRT.manifest"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall
Source: ".\dll\Microsoft.VC90.MFC.manifest"; DestDir: "{app}"; Components: program; Flags: sharedfile restartreplace uninsneveruninstall

;MSXML
Source: ".\msxml\msxml3.dll"; DestDir: "{sys}"; Components: program; Flags: recursesubdirs onlyifdoesntexist uninsneveruninstall regserver
Source: ".\msxml\msxml3a.dll"; DestDir: "{sys}"; Components: program; Flags: recursesubdirs onlyifdoesntexist uninsneveruninstall
Source: ".\msxml\msxml3r.dll"; DestDir: "{sys}"; Components: program; Flags: recursesubdirs onlyifdoesntexist uninsneveruninstall

;---ЗАЩИТА---
Source: ".\Drivers\x32\GrdDriversRU.msi"; DestDir: "{app}\Active"; Components: guardant; Check: not IsWin64; Flags: ignoreversion
Source: ".\Drivers\x64\GrdDriversRU.msi"; DestDir: "{app}\Active"; Components: guardant; Check: IsWin64; Flags: ignoreversion
Source: ".\Drivers\GrdDiag.exe"; DestDir: "{app}\Active"; Components: guardant; Flags: ignoreversion
Source: ".\Drivers\GsRemote.exe"; DestDir: "{app}\Active"; Components: guardant; Flags: ignoreversion
Source: ".\Drivers\GrdVkc32.dll"; DestDir: "{app}\Active"; Components: guardant; Flags: ignoreversion
Source: ".\Drivers\instruction.pdf"; DestDir: "{app}\Active"; Components: guardant; Flags: ignoreversion

Source: ".\License\License.txt"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\History\WhatsNew.txt"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Release\WinPos.ini"; DestDir: "{sd}\Mera Files\WinPOS"; Components: program; Flags: ignoreversion

;хелпы
Source: "..\Debug\WinPos.chm"; DestDir: "{app}"; Components: help; Flags: ignoreversion
; Есть в версии Pro:
Source: "..\Debug\Vbscript.chm"; DestDir: "{app}"; Components: help; Flags: ignoreversion
;примеры
Source: "..\Debug\WinPos.wps"; DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Debug\Samples\VBS\WPSample1.wps"; DestDir: "{sd}\Mera Files\WinPOS\Samples\VBS"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\VBS\WPSample2.wps"; DestDir: "{sd}\Mera Files\WinPOS\Samples\VBS"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\VBS\WPSample3.wps"; DestDir: "{sd}\Mera Files\WinPOS\Samples\VBS"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\VBS\WPSample4.wps"; DestDir: "{sd}\Mera Files\WinPOS\Samples\VBS"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\VBS\WPSample5.wps"; DestDir: "{sd}\Mera Files\WinPOS\Samples\VBS"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiCommon\Winpos_ole_TLB.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiCommon"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiCommon\POSBase.pas"; DestDir: "{app}\Samples\DelphiCommon"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiCommon\Winpos_ole_TLB.pas"; DestDir: "{app}\Samples\DelphiCommon"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiCommon\POSBase.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiCommon"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\WPSample1.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\WPSample2.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\WPSample3.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\WPSample4.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\WPSample5.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\WPSample6.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\Unit1.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\Unit1.dfm"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\Project1.dof"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\Project1.res"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\Project1.cfg"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\Project1.dpr"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\Project1.exe"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Unit1.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Project1_TLB.pas"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Project1.cfg"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Project1.dll"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Project1.dof"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Project1.dpr"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Project1.tlb"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\Project1.res"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\DelphiPlugIn\toolbar.res"; DestDir: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugIn"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\SampleParam.dat"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion
Source: "..\Debug\Samples\Delphi\SampleData.mera"; DestDir: "{sd}\Mera Files\WinPOS\Samples\Delphi"; Components: samples; Flags: ignoreversion

Source: "..\Debug\PlugIns\Generator\SignalGenerator.dll"; DestDir: "{app}\PlugIns\Generator"; Components: program; Flags:  ignoreversion regserver
Source: "..\Debug\PlugIns\Generator\Gen_random.dfm"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\Gen_sinus.dfm"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\Gen_random.pas"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\Gen_sinus.pas"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\Generator.pas"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\SignalGenerator.cfg"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\SignalGenerator.dof"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\SignalGenerator.dpr"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\SignalGenerator.res"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\SignalGenerator.tlb"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\SignalGenerator_TLB.pas"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion
Source: "..\Debug\PlugIns\Generator\toolbar.res"; DestDir: "{app}\PlugIns\Generator\Src"; Components: samples; Flags: ignoreversion

Source: "..\Debug\PlugIns\Reporter\Reporter.dll"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion regserver
Source: "..\Debug\PlugIns\Reporter\EasyReport.pas"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\Reporter\Reporter.cfg"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\Reporter\Reporter.dof"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\Reporter\Reporter.dpr"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\Reporter\Reporter.res"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\Reporter\Reporter.tlb"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\Reporter\Reporter_TLB.pas"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\Reporter\toolbar.res"; DestDir: "{app}\PlugIns\Reporter"; Components: program; Flags: ignoreversion

Source: "..\Debug\PlugIns\MinMax\MinMax.dll"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion regserver
Source: "..\Debug\PlugIns\MinMax\MinMax.cfg"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\MinMax\MinMax.dof"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\MinMax\MinMax.dpr"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\MinMax\MinMax.res"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\MinMax\MinMax.tlb"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\MinMax\MinMaxSearch.pas"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\MinMax\MinMax_TLB.pas"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion
Source: "..\Debug\PlugIns\MinMax\toolbar.res"; DestDir: "{app}\PlugIns\MinMax"; Components: program; Flags: ignoreversion

Source: "..\Debug\PlugIns\TimeZone\TimeZone.dll"; DestDir: "{app}\PlugIns\TimeZone"; Components: program; Flags: ignoreversion regserver

Source: "..\Debug\ImpExp\wav\wpie_wav.dll"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion regserver
Source: "..\Debug\ImpExp\wav\wav.pas"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\wav\wpie_wav.cfg"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\wav\wpie_wav.dof"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\wav\wpie_wav.dpr"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\wav\wpie_wav.res"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\wav\wpie_wav.tlb"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\wav\wpie_wav_TLB.pas"; DestDir: "{app}\PlugIns\ImpExp\wav"; Components: program; Flags:  ignoreversion

Source: "..\Debug\ImpExp\uff\wpie_uff.dll"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion regserver
Source: "..\Debug\ImpExp\uff\uff.pas"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\uff\wpie_uff.cfg"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\uff\wpie_uff.dof"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\uff\wpie_uff.dpr"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\uff\wpie_uff.res"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\uff\wpie_uff.tlb"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion
Source: "..\Debug\ImpExp\uff\wpie_uff_TLB.pas"; DestDir: "{app}\PlugIns\ImpExp\uff"; Components: program; Flags:  ignoreversion

; Есть в версии VIBRO:
Source: "..\Debug\xls\table.ini"; DestDir: "{app}\xls"; Components: program; Flags: ignoreversion
Source: "..\Debug\xls\table.xls"; DestDir: "{app}\xls"; Components: program; Flags: ignoreversion

;мануалы
Source: "{src}\WPUsersGuide.pdf"; DestDir: "{app}"; Components: manualu; Flags: external skipifsourcedoesntexist ignoreversion
Source: "{src}\WPProgrammersGuide.pdf"; DestDir: "{app}"; Components: manualp; Flags: external skipifsourcedoesntexist ignoreversion
;дополнительная инфа
Source: "{src}\MERACatalogue\MERA_Catalogue.pdf"; DestDir: "{pf}\MERA\MERACatalogue"; Components: info\catalog; Flags: external skipifsourcedoesntexist ignoreversion
;Source: "{src}\MERAPresentation\*.*"; DestDir: "{pf}\MERA\MERAPresentation\"; Components: info\present; Flags: external skipifsourcedoesntexist ignoreversion

[INI]
Filename: "{app}\WinPos.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.nppmera.ru/"

[Icons]
Name: "{group}\WinПОС"; Filename: "{app}\WinPos.exe"
Name: "{group}\WinПОС в Интернет"; Filename: "{app}\WinPos.url"
Name: "{group}\Что нового в WinПОС"; Filename: "{app}\WhatsNew.txt"
Name: "{group}\Удалить WinПОС"; Filename: "{uninstallexe}"
Name: "{userdesktop}\WinПОС"; Filename: "{app}\WinPos.exe"; MinVersion: 4,4; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\WinПОС"; Filename: "{app}\WinPos.exe"; MinVersion: 4,4; Tasks: quicklaunchicon

[Run]
Filename: "{app}\Active\GrdDriversRU.msi"; Components: guardant; Parameters: "/qn"; WorkingDir: "{app}\Active"; StatusMsg: "Установка драйверов электронного ключа..."; Flags: shellexec waituntilterminated
;Filename: "{sys}\regsvr32.exe"; Parameters: "/S SignalGenerator.dll"; WorkingDir: "{app}\PlugIns\Generator"; StatusMsg: "Регистрация подключаемых модулей..."
Filename: "{app}\WinPos.exe"; Description: "Запустить WinPos"; Flags: postinstall skipifsilent

[InstallDelete]
Type: files; Name: "{group}\WinPos.lnk"
Type: files; Name: "{group}\WinPos в Интернет.lnk"
Type: files; Name: "{group}\Что нового в WinПОС.lnk"
Type: files; Name: "{group}\Удалить WinPos.lnk"
Type: files; Name: "{userdesktop}\WinPOS.lnk";
Type: files; Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\WinPOS.lnk"
; Подчистка за старыми версиями
Type: files; Name: "{app}\Active\clocktst.exe";
Type: files; Name: "{app}\GSRemote.exe";
Type: files; Name: "{app}\Vbscript.chi";
; Подчистка за нерусскими версиями
Type: files; Name: "{app}\WinPosENU.dll";
Type: files; Name: "{app}\wpGraphsENU.dll";
Type: files; Name: "{app}\wpOperatorsENU.dll";
Type: files; Name: "{app}\wpVibroOpersENU.dll";
Type: files; Name: "{app}\wpdENU.dll";

[UninstallRun]
Filename: "MsiExec.exe"; Components: guardant; Parameters: "/qn /X ""{app}\Active\GrdDriversRU.msi"""; StatusMsg: "Удаление драйвера защиты..."; Flags: waituntilterminated
;Filename: "{sys}\regsvr32.exe"; Parameters: "/S /U SignalGenerator.dll"; WorkingDir: "{app}\PlugIns\Generator"; StatusMsg: "Отмена регистрации подключаемых модулей..."

[UninstallDelete]
Type: files; Name: "{app}\WinPos.url"
Type: files; Name: "{app}\WinPos.log"
Type: files; Name: "{sd}\Mera Files\WinPOS\WinPos.log"
Type: files; Name: "{app}\WinPos.cfg"
Type: files; Name: "{sd}\Mera Files\WinPOS\WinPos.cfg"
Type: files; Name: "{app}\Active\_instdrv.log"
;Type: files; Name: "{app}\PlugIns\Generator\SignalGenerator.cfg"
Type: dirifempty; Name: "{app}\Active"
Type: dirifempty; Name: "{app}\Dll"
Type: dirifempty; Name: "{app}\Samples\WPS"
Type: dirifempty; Name: "{app}\Samples\Delphi"
Type: dirifempty; Name: "{app}\Samples\DelphiPlugin"
Type: dirifempty; Name: "{app}\Samples\DelphiCommon"
Type: dirifempty; Name: "{app}\Samples"
Type: dirifempty; Name: "{sd}\Mera Files\WinPOS\Samples\WPS"
Type: dirifempty; Name: "{sd}\Mera Files\WinPOS\Samples\Delphi"
Type: dirifempty; Name: "{sd}\Mera Files\WinPOS\Samples\DelphiPlugin"
Type: dirifempty; Name: "{sd}\Mera Files\WinPOS\Samples\DelphiCommon"
Type: dirifempty; Name: "{sd}\Mera Files\WinPOS\Samples"

[Registry]
Root: HKCR; Subkey: "WinPos.WinPOS\CLSID"; ValueType: string; ValueName: ""; ValueData: "{{C6DC36F2-D0D5-4AA7-8FF9-AB8BB80A0F73}"; Flags: uninsdeletekey
; ассоциации с .usm
Root: HKCR; Subkey: ".usm"; ValueType: string; ValueName: ""; ValueData: "WinPos"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".mera"; ValueType: string; ValueName: ""; ValueData: "WinPos"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".wps"; ValueType: string; ValueName: ""; ValueData: "WinPos"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "WinPos"; ValueType: string; ValueName: ""; ValueData: "WinPos"; Flags: uninsdeletekey
Root: HKCR; Subkey: "WinPos\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\WINPOS.EXE,0"; Flags: uninsdeletekey
Root: HKCR; Subkey: "WinPos\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\WINPOS.EXE"" ""%1"""; Flags: uninsdeletekey
; пути
Root: HKLM; Subkey: "Software\MERA\WinPos"; ValueType: string; ValueName: "AppPath"; ValueData: "{app}\WINPOS.EXE"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\MERA\WinPos"; ValueType: string; ValueName: "MERAPath"; ValueData: "{pf}\MERA"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\MERA\WinPos"; ValueType: string; ValueName: "SetupPath"; ValueData: "{src}"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\MERA\WinPos\DllPlugins"; ValueType: string; ValueName: "Dllplugin1"; ValueData: "{app}\WPD.DLL"; Flags: uninsdeletekey

[INI]
;Filename: "{app}\WinPos.ini"; Section: "WinPos"; Key: "WorkDir"; String: "{commonappdata}\MERA\WinPOS"
Filename: "{sd}\Mera Files\WinPOS\WinPos.ini"; Section: "WinPos"; Key: "WorkDir"; String: "{sd}\Mera Files\WinPOS"

[Code]
{Вызывается by framework}
procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep=ssPostInstall then
  begin
    DeleteFile( ExpandConstant('{app}\WinPos_prv.cfg') );
    DeleteFile( ExpandConstant('{sd}\Mera Files\WinPOS\WinPos_prv.cfg') );
    RenameFile( ExpandConstant('{app}\WinPos.cfg'), ExpandConstant('{sd}\Mera Files\WinPOS\WinPos_prv.cfg') );
    RenameFile( ExpandConstant('{sd}\Mera Files\WinPOS\WinPos.cfg'), ExpandConstant('{sd}\Mera Files\WinPOS\WinPos_prv.cfg') );
  end;
end;

